# This is a Perl script containing cvsweb.conf overrides.
use strict;

# This script scans the /repos directory for repositories, assuming that it is a tree of multiple repositories, and then fills the @CVSrepositories array.

if (1) {
	@CVSrepositories = ();
	my $root = '/repos';
	if (-d "$root/CVSROOT") {
		# If $root contains a CVSROOT directory, then it is the root of a single repository.
		push(@CVSrepositories, 'local' => ['Repository', $root]);
	}
	else {
		# We're probably in the root of a tree containing repositories as subdirectories, so scan for them.
		my $h;
		opendir($h, $root) || die("Failed to opendir('$root'): $!");
		my @entries = readdir($h);
		closedir($h);
		my @unknown;
		my @repositories;
		foreach my $entry (sort { "\L$a" cmp "\L$b" } @entries) {
			if (($entry eq '.') || ($entry eq '..')) {
				next;
			}
			($entry) = ($entry =~ /^(.*)$/);	# untaint
			my $real = "$root/$entry";
			unless (-d $real) {
				next;
			}
			if (-d("$real/CVSROOT")) {
				push(@repositories, $entry);
			}
			else {
				push(@unknown, $entry);
			}
		}
		unless (@repositories) {
			my $error = "No CVS repositories found in $root";
			if (@unknown) {
				$error .= ".\nI did find these unknown subdirectories though: " . join(', ', @unknown);
			}
			die($error);
		}
		foreach my $entry (@repositories) {
			push(@CVSrepositories, $entry => [$entry, "$root/$entry"]);
		}

		# Optionally set the default repository
		#our $cvstreedefault = 'local';
	}
}

1;
